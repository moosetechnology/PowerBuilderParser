Class {
	#name : #PWBFamixModelGenerator,
	#superclass : #FamixFileBasedLanguageGenerator,
	#instVars : [
		'graphicComponent',
		'event',
		'function',
		'sqlQuery',
		'subRoutine',
		'variable',
		'dataWindow',
		'attribute',
		'method',
		'invocation',
		'userObject',
		'trigger',
		'abstractType',
		'customType',
		'basicType',
		'access',
		'behavioral',
		'variableAccess',
		'window',
		'majorObject',
		'structureFile',
		'functionFile',
		'menuObject',
		'application',
		'queryFile',
		'library',
		'reference',
		'ticket',
		'ticketAssociation',
		'tNamed',
		'tWithMethods',
		'sharedVariable',
		'localVariable',
		'parameter',
		'globalVariable',
		'instanceVariable',
		'argument'
	],
	#classInstVars : [
		'builder'
	],
	#category : #'PowerBuilder-Parser-Visitor'
}

{ #category : #accessing }
PWBFamixModelGenerator class >> generateMe [
	<script>
	self generate
]

{ #category : #accessing }
PWBFamixModelGenerator class >> packageName [

	^ #'Famix-PowerBuilder-Entities'
]

{ #category : #accessing }
PWBFamixModelGenerator class >> prefix [

	^ #'FamixPWB'
]

{ #category : #definition }
PWBFamixModelGenerator >> defineClasses [
	super defineClasses.
	library := builder newClassNamed: #Library.
	graphicComponent := builder newClassNamed: #GraphicComponent.
	behavioral := builder newClassNamed: #Behavioral.
	event := builder newClassNamed: #Event.
	function := builder newClassNamed: #Routine.
	sqlQuery := builder newClassNamed: #SqlQuery.
	subRoutine := builder newClassNamed: #SubRoutine.
	variable := builder newClassNamed: #Variable.
	attribute := builder newClassNamed: #Attribute.
	parameter := builder newClassNamed: #Parameter.
	sharedVariable := builder newClassNamed: #SharedVariable.
	localVariable := builder newClassNamed: #LocalVariable.
	globalVariable := builder newClassNamed: #GlobalVariable.
	instanceVariable := builder newClassNamed: #InstanceVariable.
	method := builder newClassNamed: #Function.
	trigger := builder newClassNamed: #Trigger.
	argument := builder newClassNamed: #Argument.


	"Major objects"
	majorObject := builder newClassNamed: #majorObject.
	dataWindow := builder newClassNamed: #DataWindow.
	userObject := builder newClassNamed: #UserObject.
	window := builder newClassNamed: #Window.
	structureFile := builder newClassNamed: #Structure.
	functionFile := builder newClassNamed: #GlobalFunction.
	menuObject := builder newClassNamed: #MenuObject.
	application := builder newClassNamed: #Application.
	queryFile := builder newClassNamed: #Query.

	"Type"
	abstractType := builder newTraitNamed: #AbstractType.
	customType := builder newClassNamed: #CustomType.
	basicType := builder newClassNamed: #BasicType.

	"Association"
	access := builder newClassNamed: #Access.
	invocation := builder newClassNamed: #Invocation.
	variableAccess := builder newClassNamed: #VariableAccess.
	reference := builder newClassNamed: #Reference.

	"CIM specific"
	ticket := builder newClassNamed: #Ticket.
	ticketAssociation := builder newClassNamed: #TicketAssociation
]

{ #category : #definition }
PWBFamixModelGenerator >> defineHierarchy [
	super defineHierarchy.
	tNamed --|> #TNamedEntity.
	tWithMethods --|> #TWithMethods.
	library
		--|> #TNamedEntity;
		--|> #TSourceEntity.
	graphicComponent
		--|> sourcedEntity;
		--|> #TWithAttributes;
		--|> behavioral.
	event
		--|> behavioral;
		--|> #TMethod;
		--|> #TInvocable;
		inheritsFromTrait: tNamed without: #(#mooseNameOn:).
	function
		--|> behavioral;
		inheritsFromTrait: tNamed without: #(#mooseNameOn:);
		--|> #TFunction.
	trigger
		--|> #TWithAttributes;
		--|> behavioral;
		--|> #TMethod;
		inheritsFromTrait: tNamed without: #(#mooseNameOn:).
	subRoutine
		--|> behavioral;
		--|> #TMethod;
		--|> #TInvocable;
		inheritsFromTrait: tNamed without: #(#mooseNameOn:).
	variable
		--|> #TSourceEntity;
		--|> #TNamedEntity;
		--|> #TWithTypes;
		--|> #TAccessible.
	parameter --|> variable.
	sharedVariable --|> attribute.
	globalVariable --|> attribute.
	localVariable --|> variable.
	instanceVariable --|> attribute.
	dataWindow
		--|> #TSourceEntity;
		--|> #TNamedEntity;
		--|> #TWithAttributes.
	attribute
		--|> #TSourceEntity;
		inheritsFromTrait: tNamed without: #(#mooseNameOn:);
		--|> #TAttribute.
	method
		--|> behavioral;
		--|> #TMethod;
		inheritsFromTrait: tNamed without: #(#mooseNameOn:);
		--|> #TWithParameters;
		--|> #TInvocable;
		--|> #THasSignature.
	behavioral
		--|> #TSourceEntity;
		--|> #TWithAccesses;
		--|> #TWithInvocations;
		--|> #TWithReferences.
	invocation --|> #TInvocation.
	invocation
		--|> #THasSignature;
		--|> #TSourceEntity.

	"Defining all major objects (files)"
	majorObject
		--|> #TSourceEntity;
		--|> #TNamedEntity;
		--|> #TWithAttributes;
		--|> #TWithAccesses;
		inheritsFromTrait: tWithMethods without: #(#numberOfLinesOfCode);
		--|> #TWithFunctions;
		--|> #TWithReferences.
	userObject --|> majorObject.
	userObject --|> abstractType.
	userObject --|> #TWithInvocations.
	window --|> majorObject.
	window --|> #TWithInvocations.
	dataWindow --|> majorObject.
	dataWindow --|> #TWithInvocations.
	structureFile --|> majorObject.
	functionFile
		--|> majorObject;
		--|> #TWithInvocations;
		--|> abstractType.
	menuObject --|> majorObject.
	application --|> majorObject.
	queryFile --|> majorObject.
	abstractType inheritsFromTrait: tNamed without: #(#mooseNameOn:).
	abstractType --|> #TType.
	abstractType --|> #TReferenceable.
	customType --|> abstractType.
	basicType --|> abstractType.
	access
		--|> #TSourceEntity;
		--|> #TAccess.
	variableAccess
		--|> #TSourceEntity;
		--|> #TAccess.
	reference
		--|> #TSourceEntity;
		--|> #TReference.
	attribute
		--|> #TSourceEntity;
		inheritsFromTrait: tNamed without: #(#mooseNameOn:);
		--|> #TAttribute;
		--|> #TWithTypes;
		--|> #TAccessible.
	argument
		--|> tNamed;
		--|> #TSourceEntity.
	parameter --|> #TParameter
]

{ #category : #definition }
PWBFamixModelGenerator >> defineProperties [
	majorObject property: #cyclomaticComplexity type: #Number.
	behavioral property: #cyclomaticComplexity type: #Number.
	ticket property: #number type: #Number.
	ticketAssociation property: #date type: #String.
	ticketAssociation property: #author type: #String.
	argument property: #argumentValue type: #Object.
	parameter property: #parameterType type: #String.
	
]

{ #category : #definition }
PWBFamixModelGenerator >> defineRelations [
	(variable property: #behavioral)
		*-<> (behavioral property: #variables).
	(dataWindow property: #graphicComponents)
		<>-* (graphicComponent property: #dataWindow).
	(majorObject property: #library)
		*-<> (library property: #userObjects).
	(ticketAssociation property: #majorObject)
		*- (majorObject property: #ticketAssociations).
	(ticketAssociation property: #ticket)
		*- (ticket property: #ticketAssociations).
	(invocation property: #arguments)
		<>-* (argument property: #invocation)
]

{ #category : #definition }
PWBFamixModelGenerator >> defineTraits [
	super defineTraits.
	tNamed := builder newTraitNamed: #PBTNamed.
	tWithMethods := builder newTraitNamed: #PBTWithMethods
]

{ #category : #definition }
PWBFamixModelGenerator >> newBuilder [
	^ super newBuilder
		withImportingContext;
		yourself
]
